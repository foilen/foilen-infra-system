buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }
}

plugins {
  id "com.jfrog.bintray" version "1.8.4"
}

allprojects {

  apply plugin: 'eclipse'

  group = 'com.foilen'
  version = System.getenv('VERSION')?:'master-SNAPSHOT'

}

subprojects {

  apply plugin: 'com.jfrog.bintray'
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
      url 'https://dl.bintray.com/foilen/maven'
    }
  }

  sourceCompatibility = 11

  eclipse {
    classpath {
      downloadSources = true
      downloadJavadoc = true
    }
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  artifacts {
    archives sourcesJar
    archives javadocJar
  }

}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')

  publish = true

  pkg {
    repo = 'maven'
    userOrg = 'foilen'
    name = 'com.foilen:foilen-infra-system'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/foilen/foilen-infra-system.git'
  }

  publications = []
}
